buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 18
    }
}

task ndkBuild(type: Exec, description: 'Compile JNI sources via NDK') {
    commandLine 'ndk-build',
            '-j',
            Runtime.runtime.availableProcessors(),
            'NDK_PROJECT_PATH=build',
            'NDK_APPLICATION_MK=src/main/jni/Application.mk',
            'APP_BUILD_SCRIPT=src/main/jni/Android.mk'
}

task ndkLibsToJar(
        type: Zip, dependsOn: 'ndkBuild',
        description: 'Create a JAR file of the native libs') {
    destinationDir new File(buildDir, 'libs')
    baseName 'ndk-libs'
    extension 'jar'
    from(new File(buildDir, 'libs')) { include '**/*.so' }
    into 'lib/'
}

tasks.withType(Compile) { compileTask -> compileTask.dependsOn ndkLibsToJar }

dependencies {
    compile fileTree(dir: new File(buildDir, 'libs'), include: '*.jar')
}